
dependencies {
    compile project(':seisFile') {
        transitive = true
    }
    compile "JSAP:JSAP:2.1"
    compile 'jython:jython:2.5.1'
}

group = 'edu.sc.seis'
version = '2.0beta5'

artifacts {
    archives jar
}

task tarDist(type: Tar, dependsOn: ['makeScript', 'genModels', 'wrapper']) {
    compression = Compression.GZIP
    dirName = project.name+'-'+version
    from('build') {
        include('build.gradle')
        include('settings.gradle')
        into dirName
    }
    from('build/output') {
        fileMode=0755
        include('bin/**')
        into dirName
    }
    from('build/output') {
        include('lib/**')
        into dirName
    }
    from('build/docs') {
        include('javadoc/**')
        into dirName
    }
    from('.') {
        include('gpl-3.0.txt')
        include('Maple/*')
        include('Notes/History')
        include('defaultProps')
        include('doc/**')
        include('exampleProperties')
        include('jacl/**')
        include('native/**')
        include('src/**')
        include('srl/*.pdf')
        include('gradleWrapper/**')
        include('gradlew')
        include('gradlew.bat')
        exclude('**/*.svn')
        into dirName
    }
}

task makeScript(dependsOn: copyToLib) << { task ->
    ScriptBuilder.create('taup_time', 'edu.sc.seis.TauP.TauP_Time', task.project)
    ScriptBuilder.create('taup_pierce', 'edu.sc.seis.TauP.TauP_Pierce', task.project)
    ScriptBuilder.create('taup_path', 'edu.sc.seis.TauP.TauP_Path', task.project)
    ScriptBuilder.create('taup_create', 'edu.sc.seis.TauP.TauP_Create', task.project)
    ScriptBuilder.create('taup_curve', 'edu.sc.seis.TauP.TauP_Curve', task.project)
    ScriptBuilder.create('taup_setsac', 'edu.sc.seis.TauP.TauP_SetSac', task.project)
    ScriptBuilder.create('taup_table', 'edu.sc.seis.TauP.TauP_Table', task.project)
    ScriptBuilder.create('taup', 'edu.sc.seis.TauP.TauP', task.project)
    ScriptBuilder.create('taup_console', 'edu.sc.seis.TauP.TauP_Console', task.project)
}


task genModels(dependsOn: compileJava) << {
    println "Generate models"
    def dir = new File(getProjectDir(), 'src/main/resources/edu/sc/seis/TauP/StdModels/')
    String[] tvelModelNames = ['iasp91', 'ak135']
    String[] ndModelNames = ['prem']
    def classLoader = new GroovyClassLoader(Project.class.classLoader)
    classLoader.addURL(new File(getBuildDir(), '/classes/main').toURL())
    configurations.default.each { File file -> classLoader.addURL(file.toURL())}
    def taupCreate = classLoader.loadClass('edu.sc.seis.TauP.TauP_Create').newInstance()
    taupCreate.setDirectory(dir.getPath())
    taupCreate.setVelFileType('tvel')
    tvelModelNames.each { String model ->
        taupCreate.setModelFilename(model)
        vMod = taupCreate.loadVMod()
        tMod = taupCreate.createTauModel(vMod)
        tMod.writeModel(new File(dir, model+'.taup').path)
    }
    taupCreate.setVelFileType('nd')
    ndModelNames.each { String model ->
        taupCreate.setModelFilename(model)
        vMod = taupCreate.loadVMod()
        tMod = taupCreate.createTauModel(vMod)
        tMod.writeModel(new File(dir, model+'.taup').path)
    }
}

assemble.dependsOn(tarDist)
processResources.dependsOn(genModels)

